[Report: AP Tally Export Master Request]
	Use: Rpc Request	
	Local: Field: Rpc Request Method: Set as: "tally_export_master"

[Collection: AP Tally Export Master Data Source]
	Use				: Rpc Data Source
	RemoteRequest	: AP Tally Export Master Request: UTF8
	JSON Object Path: "data:1"
	
;; Ref: https://tdlexpert.com/index.php?threads/some-useful-string-functions.4187/
[Function:Substr]	
	Parameter:sSource:String
	Parameter:nStart:Number
	Parameter:nLen:Number
	Returns:String

	100: Return: $$StringPart:##sSource:(##nStart-1):##nLen
	
[Function: AP Download Masters ]
	Variable: ApMasterDownloadTables : String	
	
	Variable: IdVar				: Number	
	Variable: RowId				: Number
	
	LIST VARIABLE 	: Group Map List 		: String
	LIST VARIABLE 	: VoucherType Map List 	: String

	Local Formula: Lc: $$ListCount:LedgerMapList
	
	00100:	Query Box: "Do you want to download masters" : Yes: No
	00200:  If: $$LastResult			
	00300: 	 	START BLOCK
	00400: 	   	Set Object: (Company, ##SVCurrentCompany).
	00500: 	   	LIST FILL : GroupMapList 				: APGroups 				: $APId 		: $APTallyName
	00600: 	   	LIST FILL : VoucherTypeMapList 	: APVoucherTypes 	: $APId 		: $APTallyName
	00700: 	 	END BLOCK
	
	00800:	 	Set: ApMasterDownloadTables: "voucher_type,account_type"
	
	00900: 	 	Walk Collection: ApMasterDownload Data Source
	01000: 			SET TARGET	: (Company, ##SVCurrentCompany).		

	01010:			Start Batch Post: 100
	01100: 			Start Progress	: ($$NumItems:voucher_types):"Downloading Auditplus Voucher Types"
	01200: 			Walk Collection: voucher_types
	01300:      	Show Progress:$$LoopIndex:$$Sprintf:"VoucherType Name: %s":$name
	01400: 				If: $$ListFind:VoucherTypeMapList:$$String:$id
	01500: 					Modify Object : (Company, ##SVCurrentCompany).ApVoucherTypes[1, $APId=$id].APName : $name
	01600: 				Else
	01700: 					INSERT COLLECTION OBJECT   : APVoucherTypes
	01800: 					SET VALUE  : APId   		: $id
	01900: 					SET VALUE  : APName 		: $name
	02000: 					SET VALUE  : APVoucherType	: $base_type
	02100: 					RESET VALUE  : APTallyName ;; 	: $$SysName:NotApplicable
	02200: 					Set Target: .. 
	02300: 				End If	
	02400: 			End Walk		
	02500: 			End Progress	
	02510: 			Alter Target
	02520: 			End Batch Post
	
	02530:			Start Batch Post: 100
	02600: 			Start Progress	: ($$NumItems:account_types):"Downloading Auditplus Account Types"
	02700: 			Walk Collection: account_types
	02800:      	Show Progress:$$LoopIndex:$$Sprintf:"AccountType Name: %s":$name
	02900: 				If: $$ListFind:GroupMapList:$$String:$id
	03000: 					Modify Object : (Company, ##SVCurrentCompany).ApGroups[1, $APId=$id].APName : $name
	03100: 				Else				
	03200: 					INSERT COLLECTION OBJECT   : APGroups
	03300: 					SET VALUE  : APId   		: $id
	03400: 					SET VALUE  : APName 		: $name
	03500: 					SET VALUE  : APAccountType	: $base_types
	03600: 					RESET VALUE  : APTallyName  	;; : $$SysName:NotApplicable
	03700: 					Set Target: .. 				
	03800: 				End If							
	03900: 			End Walk
	04000: 			End Progress		
	04100: 			Alter Target
	04110: 			End Batch Post
	
	04200: 		End Walk		
	04300:	End If

