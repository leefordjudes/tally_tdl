[Collection: TallyLedgers]
	Type: Ledger
	Title: "Tally Ledgers"
	Format	: $Name, 40
	
[Report: AP Ledger Map]
	Use: AP Unit Map
	Title: "Auditplus Ledger Mapping"
	
	Local: Form: APUnitMap: Delete: Buttons
	Local: Form: APUnitMap: Add: Buttons: AP Delete All Ledger
	Local: Form: APUnitMap: Add: Buttons: AP Create All Ledger
	Local: Form: APUnitMap: Add: Buttons: AP Modify Ledger
	
	Local: Part: APUnitMap: Repeat: APUnitMap: AP Ledgers
	
	Local: Line: AP Unit Map Title: Local: Field : AP Id Field		    : Info: "Id"
	Local: Line: AP Unit Map Title: Local: Field : AP Id Field		    : Width:10
	Local: Line: AP Unit Map Title: Local: Field : AP Name Field		: Info: "Name"
	Local: Line: AP Unit Map Title: Local: Field : AP Name Field		: Width:30
	Local: Line: AP Unit Map Title: Local: Field : AP Uqc Field			: Info: "Account Type"
	Local: Line: AP Unit Map Title: Local: Field : AP Uqc Field			: Width:30
	Local: Line: AP Unit Map Title: Local: Field : AP Tally Name Field	: Info: "Tally Ledger"
	Local: Line: AP Unit Map Title: Local: Field : AP Tally Name Field	: Width:30
		
	Local: Line: AP Unit Map : Local: Field: AP Id Field 		: Info	: $APId
	Local: Line: AP Unit Map : Local: Field: AP Id Field 		: Width	:10
	Local: Line: AP Unit Map : Local: Field: AP Name Field 		: Info	: $APName
	Local: Line: AP Unit Map : Local: Field: AP Name Field 		: Width	:30
	Local: Line: AP Unit Map : Local: Field: AP Uqc Field 		: Info	: $APAccountType
	Local: Line: AP Unit Map : Local: Field: AP Uqc Field 		: Width	:30
	Local: Line: AP Unit Map : Local: Field: AP Tally Name Field : Delete: Table
	Local: Line: AP Unit Map : Local: Field: AP Tally Name Field : Table: TallyLedgers, NotApplicable
;	Local: Line: AP Unit Map : Local: Field: AP Tally Name Field : Add: Key: Create Ledger
	Local: Line: AP Unit Map : Local: Field: AP Tally Name Field : Set As: $APTallyName
	Local: Line: AP Unit Map : Local: Field: AP Tally Name Field : Width:30


[Collection: APLedgers]
	Type		: APLedgers		:	Company
;	Filter		: NOT FilterMapped
	Child of    : ##SVCurrentCompany

[Function: AP Delete ALL Ledger]
	010: Set Object: (Company, ##SVCurrentCompany).	 
	020: Set Target
	030: Walk Collection: APLedgers
		040: Delete Collection Object : APLedgers
	050: End Walk
	060: Alter Target
	
[Function: AP Ledger Create Fn]
	Variable: ProgressCount		: Number	
	
	Variable: ApLedGroupName: String	

	LIST VARIABLE 	: ApGroupMapList 		: String
	LIST VARIABLE 	: TallyLedgerMapList 	: String
	LIST VARIABLE 	: LedgerMapList 		: String
	
	Local Formula : BBYR: $$YearOfDate:@@ApBookBegin
	Local Formula : BBZM: $$ZeroFill:($$MonthOfDate:@@ApBookBegin):2
	Local Formula : BBZD: $$ZeroFill:($$DayofDate:@@ApBookBegin):2
	Variable: ApApplicableFrom : String: ($$String:@BBYR)+($$String:@BBZM)+($$String:@BBZD)
	
	Local Formula: TallyGroupName: ##ApGroupMapList[$$ListIndex:ApGroupMapList:##ApLedGroupName]
	Local Formula: LedCount: $$ListCount:LedgerMapList
	
	00010: Query Box: "Do you want to create ledgers" : Yes: No
	00020: If: $$LastResult
	00100:  Set Object: (Company, ##SVCurrentCompany).	 
	
	00101: 	LIST FILL : LedgerMapList 	: APLedgers : $APId   : $APName	
	00200:  LIST FILL : ApGroupMapList 	: APGroups 	: $ApName : $APTallyName
	00210:  LIST FILL : TallyLedgerMapList : TallyLedgers : $name : $name
		 
	00220: 	START PROGRESS	: @LedCount:"Creating Masters":"Ledgers"
	00230:  SET: ProgressCount: 1
	00300:  Walk Collection : APLedgers	
	00310:    SHOW PROGRESS: ##ProgressCount
	00320:     If: $$ListFind:TallyLedgerMapList:$ApName Or $ApAccountType = "Profit & Loss"
	00330:  		 Continue
	00340:	   Else	
	00600:  		Set: ApLedGroupName: $ApAccountType 
	00700:	 	If: Not $$IsEmpty:@TallyGroupName
		    
	00800:  		New Object: Ledger
	00900:	 		Set Value: Name				: $ApName
	01000:	 		Set Value: Parent			: @TallyGroupName
	01001:	 		If: Not $$IsEmpty:$ApPanNo	
	01002:  			Set Value: INCOMETAXNUMBER	: $ApPanNo
	01003:	 		Else 
	01004:	 			If: Not $$IsEmpty:$APGstNo
	01005:  					SET VALUE: INCOMETAXNUMBER	: $$Substr:$APGstNo:3:10
	01006:	 			End If
	01100:	 		End If					
		    
	01200:	 		Insert Collection Object: LedMailingDetails
	01300:	 		Set Value:	APPLICABLEFROM 	: 	##ApApplicableFrom 
	01400:	 		Set Value: 	MailingName		:	$ApName
	
	01510:	 		If: Not $$IsEmpty:$ApAddress
	01520:	 			Insert Collection Object: Address
	01530:	 			Set Value: 	Address	:	$ApAddress
	01540:	 			Set Target: ..
	01550:	 		End If
	
	01600:	 		If: Not $$IsEmpty:$APCountry
	01610:	 			Set Value: 	Country			:	$APCountry
	01620:	 		End If

	01700:			If: Not $$IsEmpty:$ApState
	01710: 				SET VALUE  : State		: $ApState
	01720:			End If

	01800:	 		Set Target: ..
		    
	01900:	 		Insert Collection Object: LedGstRegDetails
	02000:	 		Set Value:	APPLICABLEFROM 		: 	##ApApplicableFrom 
	02100:	 		If: Not $$IsEmpty:$APGstRegType
	02110:	 			Set Value: 	GSTREGISTRATIONTYPE :	$APGstRegType
	02120:	 		End If
	02200:	 		If: Not $$IsEmpty:$APGstLocation
	02210:	 			Set Value: 	PLACEOFSUPPLY		:	$APGstLocation
	02220:	 		End If
	02300:	 		If: Not $$IsEmpty:$APGstNo
	02310:	 			Set Value: 	GSTIN				:	$APGstNo
	02320:	 		End If
	02400:	 		Set Target: ..
	
	02450:  		Create Target
		    
	02500:     	End If
	02600:  	Set: ApLedGroupName: ""
	02800:     End If
	02801: 	   INCREMENT: ProgressCount
    02900:  End Walk
	03000: End If
	
	
[Button: AP Delete All Ledger]
	Key: F4
	Action: Call : AP Delete ALL Ledger
	Title: "Delete All"	
