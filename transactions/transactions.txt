[Include: tally_export_voucher_request.txt]

[System: Formula]
	VchTypeMappedFlt: NOT $$IsEmpty:$APTallyName	
	
[Collection: APVoucherTypesMapped]
	Use			: APVoucherTypes
	Filter		: VchTypeMappedFlt
	Title		: "Mapped Voucher Types"	
	Format		: $APId, 5	
	Format		: $APTallyName, 20
		
[Function: APSaveVoucher]
	Variable: SkippedVch 		: Number : 0
	Variable: CreatedVch 		: Number : 0
	Variable: IsLedgerAvailabilityVerified	: Logical: Yes

	Local Formula: VtypName: ##VoucherTypeMapList[$$ListIndex:VoucherTypeMapList:##TxnVchTypeId]
	
	00100:  START BLOCK
	00200:		Call: APVerifyLedgerAvailabilityFn
	00300:  END BLOCK
	00400:  If: ##IsLedgerAvailabilityVerified = Yes
	00500:		Walk Collection: TallyExportVoucher Data Source
	00600: 			START PROGRESS: ($$NumItems:data):"Import Auditplus Voucher":"Creating vouchers"
	00700:  	 	Walk Collection: data
	00800:				SHOW PROGRESS: $$LoopIndex
	00900: 				call : APSaveEachVoucher				
	01000:  	 	End Walk
	01100: 		End Walk
	01200: 		Call: AP Update Map
	01300: 		Msg Box: "Result": ($$String:##CreatedVch) +" "+@VtypName+" Vouchers\n Successfully Created"
	01400: 		If: ##SkippedVch > 0 
	01500: 			Msg Box: "Skipped Vouchers": ##SkippedVch
	01600: 		End If		
	01700: 	End If		
	

[Function: GetPartyGstRegistrationType]
		Parameter	: RegType	: String
		Variable	: Out		: String
		Returns		: String		

	0100: If: ##RegType = "UNREGISTERED"
	0200:  	Set: Out: "Unregistered/Consumer"	
	0300: End If
	0400: If: ##RegType = "REGULAR"
	0500:  	Set: Out: "Regular"
	0600: End If
	0700: If: Not $$IsEmpty:$gst_reg_type And $gst_reg_type = "COMPOSITE"
	0800:  	SET VALUE  : GSTREGISTRATIONTYPE	: "Composition"
	0900: End If
	1000: Return: ##Out
		

[Variable: AP Master Map]
	Variable: Id			: Number
	Variable: TallyName		: String

[Function: APSaveEachVoucher]
	Variable: IdVar				:	Number
	Variable: ListIdxVar		: 	Number

	Variable: TallyNameVar		:	String
	Variable: TallyStockItemVar	:	String
	Variable: MapItemTypeVar 	: 	String	
	Variable: HsnCodeVar		: 	String	
	Variable: GstRegTypeVar		: 	String
	Variable: ApGroupNameVar	: 	String
	
	Variable: CreateVch			:  Logical : Yes
	
	List Variable: LedgerToSaveVar: Ap Master Map 
	
	Local Formula: TallyAccGroupName: ##TallyLedgerMapList[$$ListIndex:TallyLedgerMapList:##TallyNameVar]
	Local Formula: ApGroupMappedName: ##ApGroupMapList[$$ListIndex:ApGroupMapList:##ApGroupNameVar]
	Local Formula: PartyGstRegType: $$GetPartyGstRegistrationType:##GstRegTypeVar
	Local Formula: rate: $rate
	Local Formula: qty: $qty * -1	
	
	;Local Formula: lc: $$ListIndex:LedgerMapList:##IdVar

	00100:	START BATCH POST : 100
	00200:  If: ##TxnAccountOnly = Yes Or $mode = "ACCOUNT" Or $mode = "GST"
	00300:  	SET		:	SVViewName 	: $$SysName:AcctgVchView
	00400:  Else
	00500:  	SET		: SVViewName 	: $$SysName:InvVchView
	00600:  End If
	00700:  NEW OBJECT 	: Voucher	
	00800:  Set Value		: ApIsImported			: Yes
	00900:  SET VALUE 	: Date 							: $date	
	01000:  SET VALUE 	: VoucherTypeName 	: ##VoucherTypeMapList[$$ListIndex:VoucherTypeMapList:##TxnVchTypeId]
	01100:  SET VALUE 	: VoucherNumber			: $voucher_no
	01200: 	SET VALUE 	: PartyLedgerName 	: $party_name
	01300: 	SET VALUE 	: IsInvoice 				: No
	01400: 	If: $mode = "INVENTORY" And ##TxnAccountOnly = No
	01500: 		SET VALUE : IsInvoice 				: Yes	
	01600: 	End If
	01700: 	If: Not $$IsEmpty:$ref_no
	01800: 		SET VALUE 	: Reference			: $ref_no	
	01900: 	End If 
	02000: 	If: ( $base_voucher_type = "SALE" Or $base_voucher_type = "PURCHASE" Or $base_voucher_type = "CREDIT_NOTE" Or $base_voucher_type = "DEBIT_NOTE" )
	02100: 		SET VALUE 	: GstRegistrationType		: "Unregistered/Consumer"
	02200: 		If:  Not $$IsEmpty:$gst_reg_type
	02300:  		SET		    : GstRegTypeVar				: $gst_reg_type	
	02400:  		SET VALUE : GstRegistrationType	: @PartyGstRegType
	02500: 		End If
	02600: 	End If
	02700: 	If: NOt $$IsEmpty:$gst_no
	02800: 		Set Value	: PartyGstIn					: $gst_no
	02900: 	End If
	03000: 	SET VALUE 	: BasicBuyerName			: $party_name	
	03100: 	SET VALUE 	: PartyMailingName		: $party_name
	03200: 	SET VALUE 	: StateName						: @@GetCompanyStateName
	03300: 	SET VALUE 	: CountryOfResidence	: @@GetCompanyCountryName
	03400: 	If: Not $$IsEmpty:$gst_location
	03500: 		SET VALUE 	: PlaceOfSupply			: $gst_location
	03600: 	Else	
	03700: 		SET VALUE 	: PlaceOfSupply			: @@GetCompanyStateName
	03800: 	End If
	03900: 	If: Not $$IsEmpty:$description
	04000: 		SET VALUE 	: NARRATION					: $description+", Imported From Auditplus"
	04100: 	Else
	04200: 		SET VALUE 	: NARRATION					: "Imported From Auditplus"
	04300: 	End If
	04400: 	SET VALUE 		: EFFECTIVEDATE			: $eff_date
	04500: 	Walk Collection: ac_trns
	04600: 		If: Not $$ListFind:TallyLedgerMapList:$account_name
	04700:			Msg Box: "Missing Account" : "Account "+$account_name+" not found"
	04800:  	 	Increment: SkippedVch
	04900:  	 	Set: CreateVch: No
	05000:			Break
	05100:		End If
	05200: 		If: Not $$ListFind:ApGroupMapList:$account_type_name
	05300:			Msg Box: "Missing Ap Account Group" : "Account Group "+$account_type_name+" not found"
	05400:  	 	Increment: SkippedVch
	05500:  	 	Set: CreateVch: No
	05600:			Break
	05700:		End If
	05800: 		Set: TallyNameVar: If $account_id != 20 Then $account_name Else "Profit & Loss A/c"
	05900: 		Set: ApGroupNameVar: If $account_id != 20 Then $account_type_name Else "Primary" 
	06000: 		If: $account_id != 20 And @TallyAccGroupName != @ApGroupMappedName
	06100:			Msg Box: "Mismatch Group Map" : "Tally ledger group & auditplus group map - Mismatched"
	06200:  	 	Increment: SkippedVch
	06300:  	 	Set: CreateVch: No
	06400:			Break
	06500:		End If		
	06600: 		Set: HsnCodeVar: $hsn_code
	06700: 		If: $$IsEmpty:##HsnCodeVar		
	06800: 			INSERT COLLECTION OBJECT 				: LEDGERENTRIES
	06900: 			SET VALUE 	: LedgerName 				: ##TallyNameVar
	07000: 			SET VALUE 	: IsDeemedPositive 	: if $amount > 0 then Yes else No
	07100: 			SET VALUE 	: Amount 						: $$AsAmount:$amount * -1 
	07200: 			SET TARGET 	: ..					
	07300: 		Else				
	07400: 			Set: ListIdxVar: $$ListIndex:StockItemMapList:##HsnCodeVar
	07500: 			Set: TallyStockItemVar: ##StockItemMapList[##ListIdxVar]  		
	07600: 			If: $$IsEmpty:##TallyStockItemVar 
	07700: 				Set: MapItemTypeVar : "StockItem"
	07800: 				Alter: AP Map Tally Name
	07900: 				If: Not $$IsEmpty:##TallyStockItemVar
	08000: 					Set: StockItemMapList[##ListIdxVar]: ##TallyStockItemVar
	08100: 					List Add:StockItemAddList:##HsnCodeVar:##TallyStockItemVar
	08200: 				End if
	08300: 			End If
	08400: 			INSERT COLLECTION OBJECT 		: INVENTORYENTRIES
	08500: 			SET VALUE 	: StockItemName 	: ##TallyStockItemVar
	08600: 			SET VALUE 	: IsDeemedPositive 	: No
	08700: 			SET VALUE 	: ActualQty 		: ($$TgtObject:$$AsQty:@qty)
	08800: 			SET VALUE 	: BilledQty 		: ($$TgtObject:$$AsQty:@qty)
	08900: 			SET VALUE 	: Rate 				: ($$TgtObject:$$AsRate:@rate)
	09000: 			SET VALUE 	: Amount 			: $$AsAmount:$taxable_amount
	09100: 			SET VALUE 	: GstHsnName		: $hsn_code
	09200: 			Set Value	: GstHsnInferApplicability : "Specify Details Here"
	09300: 			Set Value	: GstRateInferApplicability : "Specify Details Here"
	09400: 			Set Value	: GstOvrdnTaxability : "Taxable"				
	09500: 			INSERT COLLECTION OBJECT 		: ACCOUNTING ALLOCATIONS
	09600: 			SET VALUE 	: LedgerName 		: ##TallyNameVar
	09700: 			SET VALUE 	: IsDeemedPositive 	: No
	09800: 			SET VALUE 	: Amount 			: $$AsAmount:$amount * -1
	09900: 			SET TARGET 	: ..
	10000: 			Insert Collection Object: RATEDETAILS
	10100: 			Set Value: GSTRATEDUTYHEAD : "CGST"
	10200: 			Set Value: GSTRATEVALUATIONTYPE : "Based on Value"			
	10300: 			Set Value: GSTRATE: ($tax_ratio / 2)
	10400: 			Set Target: ..
	10500: 			Insert Collection Object: RATEDETAILS
	10600: 			Set Value: GSTRATEDUTYHEAD : "SGST/UTGST"
	10700: 			Set Value: GSTRATEVALUATIONTYPE : "Based on Value"			
	10800: 			Set Value: GSTRATE: ($tax_ratio / 2)
	10900: 			Set Target: ..		
	11000: 			Insert Collection Object: RATEDETAILS
	11100: 			Set Value: GSTRATEDUTYHEAD : "IGST"
	11200: 			Set Value: GSTRATEVALUATIONTYPE : "Based on Value"			
	11300: 			Set Value: GSTRATE: $tax_ratio
	11400: 			Set Target: ..			
	11500: 			Insert Collection Object: RATEDETAILS
	11600: 			Set Value: GSTRATEDUTYHEAD : "CESS"
	11700: 			Set Value: GSTRATEVALUATIONTYPE : $$SysName:NotApplicable			
	11800: 			Set Target: ..		
	11900: 			Insert Collection Object: RATEDETAILS
	12000: 			Set Value: GSTRATEDUTYHEAD : "STATE CESS"
	12100: 			Set Value: GSTRATEVALUATIONTYPE : "Based on Value"			
	12200: 			Set Target: ...
	12300:  	End If
	12400:  	Set: HsnCodeVar:""
	12500:	End Walk
	12600: 	SET VALUE   	: PersistedView 	: ##SVViewName
	12700: 	If: ##CreateVch
	12800: 		CREATE TARGET
	12810: 		If: Not $$LastResult
	12820: 			Msg Box: "Voucher Creating Error": $$LastError
	12830: 		Else
	12840: 			Increment: CreatedVch
	12900: 		End if
	13000: 	End if
	13100: 	END BATCH POST

	
[Function: AP Update Map]
	Variable: LpIdx: Number: 1
	
	100: Set Object: (company, ##svcurrentcompany).
	200: Set target
	300: Walk Collection: ApLedgers				
	400: 	If: $$ListFind:LedgerAddList:$$String:$ApId			
	500: 		Set Target: APLedgers[##LpIdx]
	600: 		Set Value: ApTallyName: ##LedgerAddList[$$ListIndex:LedgerAddList:$$String:$ApId]
	700: 		Set Target: ..			
	800: 	End if		
	900: 	Increment: LpIdx
	1000: End Walk
	1100: Set: LpIdx: 1
	1200: Walk Collection: APStockItems
	1300: 	If: $$ListFind:StockItemAddList:$ApHsnCode
	1400: 		Set Target: APStockItems[##LpIdx]
	1500: 		Set Value: ApTallyName: ##StockItemAddList[$$ListIndex:StockItemAddList:$ApHsnCode]
	1600: 		Set Target: ..			
	1700: 	End if		
	1800: 	Increment: LpIdx
	1900: End Walk
	2000: Alter target

[Function: AP Map Tally Name Submit]		
	100: If: ##MapItemTypeVar = "Ledger"
	200: 	Set: TallyNameVar:#APMapTallyName
	300: Else
	400: 	Set: TallyStockItemVar:#APMapTallyName
	500: End If
	
[Report: AP Map Tally Name]
	Form: AP Map Tally Name
	
[Form: Ap Map Tally Name]
	Parts: Form SubTitle, AP Map Info
	No Confirm	: Yes
	FullWidth   : No
	Space Left	: 2
	Space Right	: 2
	On: Form Accept: Yes: Call: AP Map Tally Name Submit
	Local       : Field : Form SubTitle : Info  : $$LocaleString:"Map Information for " + ##MapItemTypeVar

[Part: AP Map Info]
	Option: AP Map Ledger Info 		: ##MapItemTypeVar = "Ledger"
	Option: AP Map Stock Item Info 	: ##MapItemTypeVar = "StockItem"
	
[!Part: AP Map Ledger Info]
	Use: Ap Map Info 1
	
[!Part: AP Map Stock Item Info]
	Use: Ap Map Info 1
	ObjectEx: (Company, ##SVCurrentCompany).ApStockItems[1, $APHsnCode=##HsnCodeVar]
	
	Local: Line: AP Map Info Line 1 : Local	:	Field	:	Medium Prompt	: Info		: "Hsn Code"
	Local: Line: AP Map Info Line 1 : Local	:	Field	:	Name Field		: Info		: $APHsnCode
	Local: Line: AP Map Info Line 2 : Local	:	Field	:	Medium Prompt	: Info		: "Description"
	Local: Line: AP Map Info Line 2 : Local	:	Field	:	Name Field		: Info		: $APName
	
[Part: Ap Map Info 1]
	Lines: Ap Map Info Line 1, Ap Map Info Line 2, AP Map Info Line3
	ObjectEx: (Company, ##SVCurrentCompany).ApLedgers[1, $APId=##IdVar]
		
	[Line: AP Map Info Line 1]
		Fields: Medium Prompt, Name Field
		Space Bottom	: 0.4
		Local	:	Field	:	Medium Prompt	: Info		: "Ledger Name"
		Local	:	Field	:	Name Field		: Info		: $APName
		
			
	[Line: AP Map Info Line 2]
		Fields: Medium Prompt, Name Field
		Space Bottom	: 0.4
		Local	:	Field	:	Medium Prompt	: Info		: "Ledger Type"
		Local	:	Field	:	Name Field 		: Info		: $APAccountType
		
	[Line: AP Map Info Line 3]
		Fields: Medium Prompt, Ap Map Tally Name
		Space Bottom	: 0.4
		Local	:	Field	:	Medium Prompt	: Info		: "Tally Name"
		
		[Field: AP Map Tally Name]
			Use: NameField 			
			Option: AP Map Tally Ledger Name : ##MapItemTypeVar = "Ledger"
			Option: AP Map Tally Unit Name : ##MapItemTypeVar = "Unit"
			Option: AP Map Tally StockItem Name : ##MapItemTypeVar = "StockItem"
			
			
		[!Field: AP Map Tally Ledger Name]
			Use: NameField
			Table: TallyLedgers,NotApplicable
			Show Table: Always
			Add: Key: Create Ledger
			
		[!Field: AP Map Tally Unit Name]
			Use: NameField
			Table: TallyUnits,NotApplicable
			Show Table: Always
			
		[!Field: AP Map Tally Stock Item Name]
			Use: NameField
			Table: TallyStockItems,NotApplicable
			Show Table: Always
			Add: Key: Create Stock Item
			
