[Include: tally_export_voucher_request.txt]

[System: Formula]
	VchTypeMappedFlt: NOT $$IsEmpty:$APTallyName	

[System: Variables]
	Variable : TxnVchFromDate 	: String	
	Variable : TxnVchToDate 	: String
	Variable : TxnVchTypeId		: Number
	Variable : TxnVchTypeName	: String
	Variable : TxnVoucherOnly	: Logical: Yes
	
	LIST VARIABLE 	: Ledger Map List 	: String
	LIST VARIABLE 	: Ledger Add List 	: String
	
	LIST VARIABLE 	: StockItem Map List 	: String
	LIST VARIABLE 	: StockItem Add List 	: String
	
	LIST VARIABLE 	: VoucherType Map List 	: String
	
	
[Collection: APVoucherTypesMapped]
	Use			: APVoucherTypes
	Filter		: VchTypeMappedFlt
	Title		: "Mapped Voucher Types"	
	Format		: $APId, 5	
	Format		: $APTallyName, 20
	
	
[Function: APSaveVoucher]
	Variable: SkippedVch : Number
	Local Formula: VtypName: ##VoucherTypeMapList[$$ListIndex:VoucherTypeMapList:##TxnVchTypeId]

	100: Walk Collection: TallyExportVoucher Data Source
;		mb2: Msg Box: $id:$voucher_no	
		200: call : APSaveEachVoucher
	300: End Walk
	400: Call: AP Update Map
	500: Msg Box: "Result":@VtypName+" Vouchers\n Successfully Created"
	600: If: ##SkippedVch > 0 
		700: Msg Box: "Skipped Vouchers": ##SkippedVch
	800: End If


[Function: GetPartyGstRegistrationType]
		Parameter	: RegType	: String
		Variable	: Out		: String
		Returns		: String		

	1: If: ##RegType = "UNREGISTERED"
	2:  Set: Out: "Unregistered/Consumer"	
	3: End If
	4: If: ##RegType = "REGULAR"
	5:  Set: Out: "Regular"
	6: End If
	7: Return: ##Out
		

[Variable: AP Master Map]
	Variable: Id			: Number
	Variable: TallyName		: String

[Function: APSaveEachVoucher]
	Variable: IdVar				:	Number
	Variable: ListIdxVar		: 	Number

	Variable: TallyNameVar		:	String
	Variable: TallyStockItemVar	:	String
	Variable: MapItemTypeVar 	: 	String	
	Variable: HsnCodeVar		: 	String	
	Variable: GstRegTypeVar		: 	String
	
	Variable: CreateVch			:  Logical : Yes
	
	List Variable: LedgerToSaveVar: Ap Master Map 
	
	Local Formula: PartyGstRegType: $$GetPartyGstRegistrationType:##GstRegTypeVar
	Local Formula: rate: $rate
	Local Formula: qty: $qty * -1	
	
	Local Formula: lc: $$ListIndex:LedgerMapList:##IdVar

	
	e10: If: $mode = "ACCOUNT" Or $mode = "GST"
	e20: 	SET  	: SVViewName 			: $$SysName:AcctgVchView
	e30: Else
	e40: 	SET		: SVViewName 			: $$SysName:InvVchView
	e50: End If
	200: NEW OBJECT : Voucher	
	300: SET VALUE 	: Date 					: $date	
	400: SET VALUE 	: VoucherTypeName 		: ##VoucherTypeMapList[$$ListIndex:VoucherTypeMapList:##TxnVchTypeId]
	410: SET VALUE 	: VoucherNumber			: $voucher_no
	500: SET VALUE 	: PartyLedgerName 		: $party_name
	600: SET VALUE 	: IsInvoice 			: No
	700: If: $mode = "INVENTORY"
	800: 	SET VALUE 	: IsInvoice 			: Yes	
	900: End If
	1000: If: Not $$IsEmpty:$ref_no
	1100: 	SET VALUE 	: Reference			: $ref_no	
	1200: End If 
	1201: If: ($mode = "INVENTORY" Or $mode = "GST") And +
			  ( $base_voucher_type = "SALE" Or $base_voucher_type = "PURCHASE" Or +
				$base_voucher_type = "CREDIT_NOTE" Or $base_voucher_type = "DEBIT_NOTE" )
	1300: 	SET VALUE 	: GstRegistrationType		: "Unregistered/Consumer"
	1400: 	If:  Not $$IsEmpty:$gst_reg_type
	1500:  		SET		    : GstRegTypeVar			: $gst_reg_type	
	1600:  		SET VALUE 	: GstRegistrationType	: @PartyGstRegType
	1700: 	End If
	1701: End If
	1800: If: NOt $$IsEmpty:$gst_no
	1900: 	Set Value	: PartyGstIn			: $gst_no
	2000: End If
;	2100: SET VALUE 	: BasicBuyerName 		: "Cash"	;; get it from first item of ac txn
	2100: SET VALUE 	: BasicBuyerName		: $party_name	
	2200: SET VALUE 	: PartyMailingName		: $party_name
	2300: SET VALUE 	: StateName				: @@GetCompanyStateName
	2400: SET VALUE 	: CountryOfResidence	: @@GetCompanyCountryName
	2500: If: Not $$IsEmpty:$gst_location
		2600: 	SET VALUE 	: PlaceOfSupply			: $gst_location
	2700: Else	
		2800: SET VALUE 	: PlaceOfSupply			: @@GetCompanyStateName
	2900: End If

	e100: If: Not $$IsEmpty:$description
		e200: 	SET VALUE 	: NARRATION			: $description		
	e300: End If
	e400: SET VALUE 	: EFFECTIVEDATE		: $eff_date
	
	3000: Walk Collection: ac_trns
		3100: Set: IdVar: $account_id  	
		3200: Set: ListIdxVar: $$ListIndex:LedgerMapList:##IdVar

		3201: If: ##ListIdxVar = 0
		3202:   Msg Box: "Account Not Available": "Account Id: "+($$string:$account_id) + "\n\n VoucherNo: " + $voucher_no + "\n\nVoucher skipped"
		3203:   Increment: SkippedVch
		3204:   Set: CreateVch: No
		3205: 	Break
		3206: End If
		3300: Set: TallyNameVar: ##LedgerMapList[##ListIdxVar]  		
		
		3400: If: $$IsEmpty:##TallyNameVar 
			3500: Set: MapItemTypeVar : "Ledger"
			3600: Alter: AP Map Tally Name
			3700: If: Not $$IsEmpty:##TallyNameVar
				3800: Set: LedgerMapList[##ListIdxVar]: ##TallyNameVar
				3900: List Add:LedgerAddList:##IdVar:##TallyNameVar
			4000: End if
		4100: End If
		
		4200: Set: HsnCodeVar: $hsn_code
		4300: If: $$IsEmpty:##HsnCodeVar		
			4400: INSERT COLLECTION OBJECT 			: LEDGERENTRIES
			4500: SET VALUE 	: LedgerName 		: ##TallyNameVar
			4600: SET VALUE 	: IsDeemedPositive 	: if $amount > 0 then Yes else No
			4700: SET VALUE 	: Amount 			: $$AsAmount:$amount * -1 
			4800: SET TARGET 	: ..					
		4900: Else				
			5000: Set: ListIdxVar: $$ListIndex:StockItemMapList:##HsnCodeVar
			5100: Set: TallyStockItemVar: ##StockItemMapList[##ListIdxVar]  		
			5200: If: $$IsEmpty:##TallyStockItemVar 
				5300: Set: MapItemTypeVar : "StockItem"
				5400: Alter: AP Map Tally Name
				5500: If: Not $$IsEmpty:##TallyStockItemVar
					5600: Set: StockItemMapList[##ListIdxVar]: ##TallyStockItemVar
					5700: List Add:StockItemAddList:##HsnCodeVar:##TallyStockItemVar
				5800: End if
			5900: End If

			6000: INSERT COLLECTION OBJECT 			: INVENTORYENTRIES
			6100: SET VALUE 	: StockItemName 	: ##TallyStockItemVar
			6200: SET VALUE 	: IsDeemedPositive 	: No
			6300: SET VALUE 	: ActualQty 		: ($$TgtObject:$$AsQty:@qty)
			6400: SET VALUE 	: BilledQty 		: ($$TgtObject:$$AsQty:@qty)
			6500: SET VALUE 	: Rate 				: ($$TgtObject:$$AsRate:@rate)
			6600: SET VALUE 	: Amount 			: $$AsAmount:$taxable_amount
			6700: SET VALUE 	: GstHsnName		: $hsn_code
			6800: Set Value		: GstHsnInferApplicability : "Specify Details Here"
			6900: Set Value		: GstRateInferApplicability : "Specify Details Here"
			7000: Set Value		: GstOvrdnTaxability : "Taxable"			
			7100: INSERT COLLECTION OBJECT 			: ACCOUNTING ALLOCATIONS
			7200: SET VALUE 	: LedgerName 		: ##TallyNameVar
			7300: SET VALUE 	: IsDeemedPositive 	: No
			7400: SET VALUE 	: Amount 			: $$AsAmount:$amount * -1
			7500: SET TARGET 	: ..
			7600: Insert Collection Object: RATEDETAILS
			7700: Set Value: GSTRATEDUTYHEAD : "CGST"
			7800: Set Value: GSTRATEVALUATIONTYPE : "Based on Value"			
			7900: Set Value: GSTRATE: ($tax_ratio / 2)
			8000: Set Target: ..
			9100: Insert Collection Object: RATEDETAILS
			9200: Set Value: GSTRATEDUTYHEAD : "SGST/UTGST"
			9300: Set Value: GSTRATEVALUATIONTYPE : "Based on Value"			
			9400: Set Value: GSTRATE: ($tax_ratio / 2)
			9500: Set Target: ..	
			9600: Insert Collection Object: RATEDETAILS
			9700: Set Value: GSTRATEDUTYHEAD : "IGST"
			9800: Set Value: GSTRATEVALUATIONTYPE : "Based on Value"			
			9900: Set Value: GSTRATE: $tax_ratio
			10000: Set Target: ..		
			10100: Insert Collection Object: RATEDETAILS
			10200: Set Value: GSTRATEDUTYHEAD : "CESS"
			10300: Set Value: GSTRATEVALUATIONTYPE : $$SysName:NotApplicable			
			10400: Set Target: ..		
			10500: Insert Collection Object: RATEDETAILS
			10600: Set Value: GSTRATEDUTYHEAD : "STATE CESS"
			10700: Set Value: GSTRATEVALUATIONTYPE : "Based on Value"			
			10800: Set Target: ...

		109000: End If
		110000: Set: HsnCodeVar:""
	11100: End Walk

	11200: SET VALUE   	: PersistedView 	: ##SVViewName
	11300: If: ##CreateVch
	11400: 		CREATE TARGET
	11500: End if

	
[Function: AP Update Map]
	Variable: LpIdx: Number: 1
	
	1: set Object: (company, ##svcurrentcompany).
	2: set target
	3: Walk Collection: ApLedgers				
		5: If: $$ListFind:LedgerAddList:$$String:$ApId			
			6: Set Target: APLedgers[##LpIdx]
			7: Set Value: ApTallyName: ##LedgerAddList[$$ListIndex:LedgerAddList:$$String:$ApId]
			8: Set Target: ..			
		9: end if		
		5a: Increment: LpIdx
	10: End Walk
	11: Set: LpIdx: 1
	12: Walk Collection: APStockItems
		13: If: $$ListFind:StockItemAddList:$ApHsnCode
			14: Set Target: APStockItems[##LpIdx]
			15: Set Value: ApTallyName: ##StockItemAddList[$$ListIndex:StockItemAddList:$ApHsnCode]
			16: Set Target: ..			
		17: end if		
		18: Increment: LpIdx
	19: End Walk
	20: alter target

[Function: AP Map Tally Name Submit]		
	100: If: ##MapItemTypeVar = "Ledger"
		200: Set: TallyNameVar:#APMapTallyName
	300: Else
		400: Set: TallyStockItemVar:#APMapTallyName
	500: End If
	

[Report: AP Map Tally Name]
	Form: AP Map Tally Name
	
[Form: Ap Map Tally Name]
	Parts: Form SubTitle, AP Map Info
	No Confirm	: Yes
	FullWidth   : No
	Space Left	: 2
	Space Right	: 2
	On: Form Accept: Yes: Call: AP Map Tally Name Submit
	Local       : Field : Form SubTitle : Info  : $$LocaleString:"Map Information for " + ##MapItemTypeVar

[Part: AP Map Info]
	Option: AP Map Ledger Info 		: ##MapItemTypeVar = "Ledger"
	Option: AP Map Stock Item Info 	: ##MapItemTypeVar = "StockItem"
	
[!Part: AP Map Ledger Info]
	Use: Ap Map Info 1
	
[!Part: AP Map Stock Item Info]
	Use: Ap Map Info 1
	ObjectEx: (Company, ##SVCurrentCompany).ApStockItems[1, $APHsnCode=##HsnCodeVar]
	
	Local: Line: AP Map Info Line 1 : Local	:	Field	:	Medium Prompt	: Info		: "Hsn Code"
	Local: Line: AP Map Info Line 1 : Local	:	Field	:	Name Field		: Info		: $APHsnCode
	Local: Line: AP Map Info Line 2 : Local	:	Field	:	Medium Prompt	: Info		: "Description"
	Local: Line: AP Map Info Line 2 : Local	:	Field	:	Name Field		: Info		: $APName
	
[Part: Ap Map Info 1]
	Lines: Ap Map Info Line 1, Ap Map Info Line 2, AP Map Info Line3
	ObjectEx: (Company, ##SVCurrentCompany).ApLedgers[1, $APId=##IdVar]
		
	[Line: AP Map Info Line 1]
		Fields: Medium Prompt, Name Field
		Space Bottom	: 0.4
		Local	:	Field	:	Medium Prompt	: Info		: "Ledger Name"
		Local	:	Field	:	Name Field		: Info		: $APName
		
			
	[Line: AP Map Info Line 2]
		Fields: Medium Prompt, Name Field
		Space Bottom	: 0.4
		Local	:	Field	:	Medium Prompt	: Info		: "Ledger Type"
		Local	:	Field	:	Name Field 		: Info		: $APAccountType
		
	[Line: AP Map Info Line 3]
		Fields: Medium Prompt, Ap Map Tally Name
		Space Bottom	: 0.4
		Local	:	Field	:	Medium Prompt	: Info		: "Tally Name"
		
		[Field: AP Map Tally Name]
			Use: NameField 			
			Option: AP Map Tally Ledger Name : ##MapItemTypeVar = "Ledger"
			Option: AP Map Tally Unit Name : ##MapItemTypeVar = "Unit"
			Option: AP Map Tally StockItem Name : ##MapItemTypeVar = "StockItem"
			
			
		[!Field: AP Map Tally Ledger Name]
			Use: NameField
			Table: TallyLedgers,NotApplicable
			Show Table: Always
			Add: Key: Create Ledger
			
		[!Field: AP Map Tally Unit Name]
			Use: NameField
			Table: TallyUnits,NotApplicable
			Show Table: Always
			
		[!Field: AP Map Tally Stock Item Name]
			Use: NameField
			Table: TallyStockItems,NotApplicable
			Show Table: Always
			Add: Key: Create Stock Item
			
