;[Variable: AP Txn Voucher Only]
;	Type		: Logical
;	Default		: Yes
;	Persistent	: Yes
	
[Report: AP Txn Configuration]
	Title	: 	Transaction Configuration
	Form	: 	AP Txn Configuration
	Object	:	Company	: ##SVCurrentCompany
	
	
[Form: AP Txn Configuration]	
	Parts		: Form SubTitle, AP Txn Configuration Body	
	FullWidth   : No
	Space Left	: 2
	Space Right	: 2
	Local       : Field : Form SubTitle : Info  : $$LocaleString:"Auditplus Transaction Configuration"
	On 			: Form Accept: Yes: LIST FILL : Ledger Map List : APLedgers : ($$String:$APId) : $APTallyName
	On 			: Form Accept: Yes: LIST FILL : StockItem Map List : APStockItems : $APHsnCode : $APTallyName
	On 			: Form Accept: Yes: LIST FILL : VoucherType Map List : APVoucherTypesMapped : $APId : $APTallyName
	On			: Form Accept: Yes: Call: APSaveVoucher

[Part: AP Txn Configuration Body]
	Line:	Ap Txn Voucher Only	
	Line:	AP Txn From Date
	Line:	AP Txn To Date
	Line:	Ap Txn Voucher Type
	
	
	[Line:	Ap Txn Voucher Only]
		Fields:	Medium Prompt, Ap Txn Voucher Only
		Space Bottom	: 0.4				
		Local	:	Field	:	Medium Prompt	: Info		: "Voucher Only"
		
		[Field: AP Txn Voucher Only]
			Use         : Logical Field
			Modifies    : Txn Voucher Only
			Set As		: If $$IsEmpty:##TxnVoucherOnly Then Yes else ##TxnVoucherOnly
			
	[Line: AP Txn From Date]
		Fields: Medium Prompt, AP Txn From Date
		Space Bottom	: 0.4				
		Local	:	Field	:	Medium Prompt	: Info		: "From Date"
		
		[Field: AP Txn From Date]
			YR: $$YearOfDate:$$SysInfo:SystemDate
			ZM: $$ZeroFill:($$MonthOfDate:$$SysInfo:SystemDate):2
			ZD: $$ZeroFill:($$DayofDate:$$SysInfo:SystemDate):2
			Width		: 20
			Max			: 250
			Type		: String
			Set as		: $$String:@YR+"-"+$$String:@ZM+"-"+$$String:@ZD
			Modifies	: TxnVchFromDate : Yes
			Style   	: Normal Bold
			Case    	: Lower Case
			

	[Line: Ap Txn To Date]
		Fields: Medium Prompt, Ap Txn To Date
		Space Bottom	: 0.4
		Local	:	Field	:	Medium Prompt	: Info		: "To Date"
		
		[Field: AP Txn To Date]	
			YR: $$YearOfDate:$$SysInfo:SystemDate
			ZM: $$ZeroFill:($$MonthOfDate:$$SysInfo:SystemDate):2
			ZD: $$ZeroFill:($$DayofDate:$$SysInfo:SystemDate):2
			Width		: 20
			Max			: 250
			Type		: String
			Set as		: $$String:@YR+"-"+$$String:@ZM+"-"+$$String:@ZD
			Modifies	: TxnVchToDate : Yes
			Style   	: Normal Bold
			Case    	: Lower Case
		
	[Line: Ap Txn Voucher Type]
		Fields: Medium Prompt, AP Txn Voucher Type
		Space Bottom	: 1
		Local	:	Field	:	Medium Prompt	: Info		: "Voucher Type"
		
		[Field: AP Txn Voucher Type]
			Width		: 20
			Max			: 250
			Modifies	: TxnVchTypeId : Yes
			Style   	: Normal Bold
			Show Table	: Always
			Table		: APVoucherTypesMapped
			Tooltip		: "Mapped voucher types"